apply {
    plugin "com.android.application"
    plugin "kotlin-android"
    plugin "kotlin-android-extensions"
}

GLog.d(project.toString() + " -> app")

android {
    compileSdkVersion Config.compileSdkVersion
    defaultConfig {
        applicationId Config.applicationId + suffix
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName
        multiDexEnabled true
        resValue "string", "app_name", Config.appName + suffix
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "8g"
        maxProcessCount 8
        dexInProcess = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api Config.depConfig.plugin.kotlin_stdlib_jdk7.dep
    api Config.depConfig.plugin.core_ktx.dep
    api Config.depConfig.support.appcompat.dep
    api Config.depConfig.support.constraintlayout.dep

    // 根据 Config.pkgConfig 来依赖所有的pkg
    for (def entrySet : ConfigUtils.getApplyPkgs().entrySet()) {
        api entrySet.value.dep
    }
}

def getSuffix() {
    if (project.path == ":feature:launcher:app") return ""
    return project.path.replace(":", "_").substring(":feature".length(),
            project.path.length() - ":app".length())
}
